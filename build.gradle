plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
}
architectury {
    minecraft = rootProject.minecraft_version
}
subprojects {
    apply plugin: "dev.architectury.loom"
    loom {
        silentMojangMappingsLicense()
    }
    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}
def isDev = rootProject.mod_version.toString().contains("SNAPSHOT") || rootProject.mod_version.toString().contains("PRE") || rootProject.mod_version.toString().contains("RC")
allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    base {
        archivesName = rootProject.mod_id
    }
    if(isDev)
        version = "${rootProject.mod_version}-${(new Date()).format('yyyyMMdd.HHmm', TimeZone.getTimeZone('EET'))}"
    else version = rootProject.mod_version
    group = rootProject.maven_group
    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 21
    }
    java {
        withSourcesJar()
    }
}